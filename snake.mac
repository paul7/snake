	.title	snake
	.ident	/X00.53/

	.mcall	exit$s,qiow$s,mrkt$,dir$,astx$s,stop$s

	.mcall	save02
	.mcall	rnd,srnd
	.mcall	gamini,onchar,onval,endtab,gamevt,gamend
	.mcall	prtat,clrscr,hidecr,showcr

	.psect	dpb,d,rw
deftmr	=	20.
mintmr	=	5.
maxtmr	=	50.
stptmr	=	5.
timer:	mrkt$	,deftmr,1,tmrast

	.psect	data,d,rw
iosb:	.blkw	2
y:	.word	20.
x:	.word	20.
newdx:	.word	0
newdy:	.word	0
dx:	.word	1
dy:	.word	0
len:	.word	3
score:	.word	0
pausef:	.word	0
stopf:	.word	0
redrwf:	.word	0
maxx	=	40.
maxy	=	40.
field:	.blkb	maxy*maxx
newhea:	.byte	0
head:	.byte	0,0
buf:	.blkb	maxx+40.

	.psect	lit,d,ro
vbord:	.asciz	/|/
hbord:	.rept	maxx
	.ascii	/-/
	.endr
	.asciz	/+/
body:	.asciz	/+/
empty:	.asciz	/ /
target:	.asciz	/#/
uhead:	.asciz	/v/
dhead:	.asciz	/^/
lhead:	.asciz	/>/
rhead:	.asciz	/</
scofmt:	.asciz	/Score: %M/

	.macro	dofld	rx,ry,rf,subr,?loopy,?loopx
	mov	#field,rf
	mov	#maxx,rx
loopx:	mov	#maxy,ry
loopy:	call	subr
	inc	rf
	sob	ry,loopy
	sob	rx,loopx
	.endm

	.psect	dispat,d,ro
distab:	onval	400,step
	onval	1000,redraw
	onchar	q,.gmend
	onchar	Q,.gmend
	onchar	4,left
	onchar	8,up
	onchar	6,right
	onchar	2,down
	onchar	p,pause
	onchar	P,pause
	onchar	r,redraw
	onchar	R,redraw
	onchar	+,faster
	onchar	-,slower
	onchar	s,stop
	onchar	S,stop
	endtab

	.psect	code,i,ro
start::	srnd
	call	inifld
	call	right
	dir$	#timer
2$:	hidecr
	gamevt	#1000
	gamini	#distab
	showcr
	tst	stopf
	bne	1$
	exit$s
1$:	mov	#-1,pausef
	stop$s
	clr	stopf
	br	2$

dscore:	save02
	mov	#buf,r0
	mov	#scofmt,r1
	mov	#score,r2
	call	$edmsg
	clrb	@r0
	prtat	#maxx+4,#4,#buf
	return

inifld:	dofld	r0,r1,r2,2$
	call	plctgt
	mov	x,r0
	mov	#maxx,r1
	call	$mul
	add	y,r1
	inc	field(r1)
	return
2$:	clrb	@r2
	return

faster:	sub	#stptmr,timer+m.ktmg	
	cmp	timer+m.ktmg,#mintmr
	bge	1$
	mov	#mintmr,timer+m.ktmg
1$:	return

slower:	add	#stptmr,timer+m.ktmg
	cmp	timer+m.ktmg,#maxtmr
	ble	1$
	mov	#maxtmr,timer+m.ktmg
1$:	return

stop:	inc	stopf
	jmp	.gmend	

redraw:	mov	pausef,-(sp)
	mov	#1,pausef
	com	redrwf
	clrscr
	call	border
	call	dscore
	call	disply
	mov	(sp)+,pausef
	return

border:	mov	#maxy,r0
1$:	prtat	#maxx+1,r0,#vbord
	sob	r0,1$
	prtat	#0,#maxy+1,#hbord
	return

pause:	com	pausef
	return

left:	clr	newdy
	mov	#-1,newdx
	movb	lhead,newhea
	br	newdir
	
right:	clr	newdy
	mov	#1,newdx
	movb	rhead,newhea
	br	newdir
	
down:	mov	#1,newdy
	clr	newdx
	movb	dhead,newhea
	br	newdir
	
up:	mov	#-1,newdy
	clr	newdx
	movb	uhead,newhea
newdir:	mov	newdx,r0
	add	dx,r0
	bne	1$
	mov	newdy,r1
	add	dy,r1
	beq	2$
1$:	mov	newdx,dx
	mov	newdy,dy
	movb	newhea,head
2$:	return

tmrast:	add	#2,sp
	tst	pausef
	bne	1$
	gamevt	#400
1$:	dir$	#timer
	astx$s

step:	call	age
	sub	dx,x
	bge	4$
	add	#maxx,x
4$:	sub	dy,y
	bge	5$
	add	#maxy,y
5$:	cmp	x,#maxx
	blt	2$
	sub	#maxx,x
2$:	cmp	y,#maxy
	blt	3$
	sub	#maxy,y
3$:	mov	x,r0
	mov	#maxy,r1
	call	$mul
	add	y,r1
	tstb	field(r1)
	bgt	oops
	beq	1$
	inc	len
	inc	score
	call	dscore
	call	plctgt
1$:	movb	#1,field(r1)
	call	disply
	return
oops:	gamend
	return

age:	dofld	r0,r1,r2,1$
	return
1$:	tstb	@r2
	ble	2$
	incb	@r2
2$:	return

plctgt:	save02
	rnd	r0,#maxx
	mov	#maxy,r1
	call	$mul
	rnd	r0,#maxy
	add	r0,r1
	tstb	field(r1)
	bne	plctgt
	movb	#-1,field(r1)
	return

disply:	dofld	r4,r3,r5,displ1
	clr	redrwf
	return
displ1:	movb	@r5,r2
	beq	skip1
	blt	tgt1
	cmp	r2,#1
	beq	head1
	cmp	r2,#2
	beq	body1
	cmp	r2,len
	bgt	empty1
	tst	redrwf
	beq	skip1
body1:	mov	#body,r1
	br	prt
empty1:	clrb	@r5
	mov	#empty,r1
	br	prt
tgt1:	cmp	r2,#-1
	beq	1$
	tst	redrwf
	beq	skip1
	br	2$
1$:	decb	@r5
2$:	mov	#target,r1
	br	prt
head1:	mov	#head,r1
prt:	prtat	r4,r3,r1
skip1:	return

	.end	start
