	.title	snake
	.ident	/X00.50/

	.mcall	exit$s,qiow$s,mrkt$,dir$,astx$s

	.mcall	save02
	.mcall	rnd,srnd
	.mcall	gamini,onchar,onval,endtab,gamevt,gamend

	.psect	dpb,d,rw
deftmr	=	20.
mintmr	=	5.
maxtmr	=	50.
stptmr	=	5.
timer:	mrkt$	,deftmr,1,tmrast

	.psect	data,d,rw
iosb:	.blkw	2
y:	.word	20.
x:	.word	20.
dispy:	.word	0
dispx:	.word	0
cur:	.word	0
dx:	.word	1
dy:	.word	0
len:	.word	3
pausef:	.word	0
redrwf:	.word	0
maxx	=	40.
maxy	=	40.
field:	.blkb	maxy*maxx
esc:	.byte	33
buf:	.blkb	80.
erase:	.ascii	<33>/[2J/
erasel	=	. - erase
hide:	.ascii	<33>/[?25l/
hidel	=	. - hide
show:	.ascii	<33>/[?25h/
showl	=	. - show
fmt:	.asciz	/[%D;%DH%A/
vbord:	.ascii	/|/
hbord:	.ascii	/-/
cbord:	.ascii	/+/
body:	.ascii	/+/
empty:	.ascii	/ /
target:	.ascii	/#/
uhead:	.ascii	/v/
dhead:	.ascii	/^/
lhead:	.ascii	/>/
rhead:	.ascii	/</
head:	.blkb	1

	.macro	dofld	rx,ry,rf,subr,?loopy,?loopx
	mov	#field,rf
	mov	#maxx,rx
loopx:	mov	#maxy,ry
loopy:	call	subr
	inc	rf
	sob	ry,loopy
	sob	rx,loopx
	.endm

	.psect	dispat,d,ro
distab:	onval	400,step
	onchar	q,.gmend
	onchar	Q,.gmend
	onchar	4,left
	onchar	8,up
	onchar	6,right
	onchar	2,down
	onchar	p,pause
	onchar	P,pause
	onchar	r,redraw
	onchar	R,redraw
	onchar	+,faster
	onchar	-,slower
	endtab

	.psect	code,i,ro
start::	srnd
	call	inifld
	call	clrscr
	call	hidecr
	call	right
	call	border
	call	disply
	dir$	#timer
	gamini	#distab
	call	showcr
	exit$s

inifld:	dofld	r0,r1,r2,2$
	call	plctgt
	mov	x,r0
	mov	#maxx,r1
	call	$mul
	add	y,r1
	inc	field(r1)
	return
2$:	clrb	@r2
	return

clrscr:	qiow$s	#io.wlb,#5,#1,,#iosb,,<#erase,#erasel>
	return

hidecr:	qiow$s	#io.wlb,#5,#1,,#iosb,,<#hide,#hidel>
	return

showcr:	qiow$s	#io.wlb,#5,#1,,#iosb,,<#show,#showl>
	return

faster:	sub	#stptmr,timer+m.ktmg	
	cmp	timer+m.ktmg,#mintmr
	bge	1$
	mov	#mintmr,timer+m.ktmg
1$:	return

slower:	add	#stptmr,timer+m.ktmg
	cmp	timer+m.ktmg,#maxtmr
	ble	1$
	mov	#maxtmr,timer+m.ktmg
1$:	return

redraw:	mov	pausef,-(sp)
	mov	#1,pausef
	com	redrwf
	call	clrscr
	call	border
	call	disply
	mov	(sp)+,pausef
	return

border:	mov	#maxy+1,r3
	mov	#maxx+1,dispx
	clr	dispy
1$:	mov	#buf,r0
	mov	#fmt,r1
	mov	#vbord,cur
	mov	#dispy,r2
	call	$edmsg
	sub	#esc,r0
	qiow$s	#io.wlb,#5,#1,,#iosb,,<#esc,r0>
	inc	dispy
	sob	r3,1$
	mov	#maxx+1,r3
	mov	#maxy+1,dispy
	clr	dispx
2$:	mov	#buf,r0
	mov	#fmt,r1
	mov	#hbord,cur
	mov	#dispy,r2
	call	$edmsg
	sub	#esc,r0
	qiow$s	#io.wlb,#5,#1,,#iosb,,<#esc,r0>
	inc	dispx
	sob	r3,2$
	mov	#buf,r0
	mov	#fmt,r1
	mov	#cbord,cur
	mov	#dispy,r2
	mov	#maxx+1,dispx
	mov	#maxy+1,dispy
	call	$edmsg
	sub	#esc,r0
	qiow$s	#io.wlb,#5,#1,,#iosb,,<#esc,r0>
	return

pause:	com	pausef
	return

left:	clr	dy
	mov	#-1,dx
	movb	lhead,head
	return
	
right:	clr	dy
	mov	#1,dx
	movb	rhead,head
	return
	
down:	mov	#1,dy
	clr	dx
	movb	dhead,head
	return
	
up:	mov	#-1,dy
	clr	dx
	movb	uhead,head
	return

tmrast:	add	#2,sp
	tst	pausef
	bne	1$
	gamevt	#400
1$:	dir$	#timer
	astx$s

step:	call	age
	sub	dx,x
	bge	4$
	add	#maxx,x
4$:	sub	dy,y
	bge	5$
	add	#maxy,y
5$:	cmp	x,#maxx
	blt	2$
	sub	#maxx,x
2$:	cmp	y,#maxy
	blt	3$
	sub	#maxy,y
3$:	mov	x,r0
	mov	#maxy,r1
	call	$mul
	add	y,r1
	tstb	field(r1)
	bgt	oops
	beq	1$
	inc	len
	call	plctgt
1$:	movb	#1,field(r1)
	call	disply
	return
oops:	gamend
	return

age:	dofld	r0,r1,r2,1$
	return
1$:	tstb	@r2
	ble	2$
	incb	@r2
2$:	return

plctgt:	save02
	rnd	r0,#maxx
	mov	#maxy,r1
	call	$mul
	rnd	r0,#maxy
	add	r0,r1
	tstb	field(r1)
	bne	plctgt
	movb	#-1,field(r1)
	return

disply:	dofld	r4,r3,r5,displ1
	clr	redrwf
	return
displ1:	movb	@r5,r2
	beq	skip1
	blt	tgt1
	cmp	r2,#1
	beq	head1
	cmp	r2,#2
	beq	body1
	cmp	r2,len
	bgt	empty1
	tst	redrwf
	beq	skip1
body1:	mov	#body,cur
	br	prt
empty1:	clrb	@r5
	mov	#empty,cur
	br	prt
tgt1:	cmp	r2,#-1
	beq	1$
	tst	redrwf
	beq	skip1
	br	2$
1$:	decb	@r5
2$:	mov	#target,cur
	br	prt
head1:	mov	#head,cur
prt:	mov	r4,dispx
	mov	r3,dispy
	mov	#buf,r0
	mov	#fmt,r1
	mov	#dispy,r2
	call	$edmsg
	sub	#esc,r0
	qiow$s	#io.wlb,#5,#1,,#iosb,,<#esc,r0>	
skip1:	return

	.end	start
